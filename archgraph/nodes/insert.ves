import "blueprint.node" for Node
import "blueprint.pin" for Pin
import "blueprint.variant" for Variant, VAR_TYPE_ARRAY
import "blueprint.variant_helper" for VariantHelper
import "blueprint.blueprint" for Blueprint
import "archgraph.variant" for VAR_TYPE_GEOMETRY
import "archgen" for ArchTools
import "maths" for Matrix44, Maths
import "math" for Math

class Insert is Node
{
	init()
	{
		super.init()

		this.imports = [
			Pin(this, "geo", [ VAR_TYPE_GEOMETRY, VAR_TYPE_ARRAY ]),
			Pin(this, "pos", [ VAR_TYPE_GEOMETRY, VAR_TYPE_ARRAY ]),
		]
		this.exports = [
			Pin(this, "geo", [ VAR_TYPE_GEOMETRY, VAR_TYPE_ARRAY ]),
		]

		this.layout()
	}

	calc_value(idx)
	{
		var v_geo = Blueprint.calc_input_value(this, 0)
		if (!v_geo or !v_geo.value) {
			return nil
		}

		var v_pos = Blueprint.calc_input_value(this, 1)
		if (!v_pos) {
			return nil
		}

		var geos = []

		var positions = VariantHelper.flatten_values(v_pos, VAR_TYPE_GEOMETRY)
		var in_geos = VariantHelper.flatten_values(v_geo, VAR_TYPE_GEOMETRY)		
		for (var pos in positions)
		{
			if (pos.scope)
			{
				for (var geo in in_geos)
				{
					var aabb = geo.mesh.aabb
					var new = geo.clone()
					new.trans.transform_mat4(ArchTools.calc_geo_mat(aabb, pos.scope))
					geos.add(Variant(VAR_TYPE_GEOMETRY, new))
				}
			}
		}

		if (geos.count > 1) {
			return Variant(VAR_TYPE_ARRAY, geos)
		} else if (geos.count == 1) {
			return geos[0]
		} else {
			return nil
		}
	}
}
import "blueprint.node" for Node
import "blueprint.pin" for Pin
import "blueprint.node_param" for NodeParam
import "blueprint.variant" for Variant, VAR_TYPE_ARRAY, VAR_TYPE_NUMBER
import "blueprint.blueprint" for Blueprint
import "archgraph.variant" for VAR_TYPE_GEOMETRY
import "archgraph.geo_helper" for GeoHelper
import "maths.vector" for Vector3
import "maths" for Matrix44

class RotateScope is Node
{
	init()
	{
		super.init()

		this.imports = [
			Pin(this, "geo", [ VAR_TYPE_GEOMETRY, VAR_TYPE_ARRAY ]),
			Pin(this, "x", VAR_TYPE_NUMBER),
			Pin(this, "y", VAR_TYPE_NUMBER),
			Pin(this, "z", VAR_TYPE_NUMBER),
		]
		this.exports = [
			Pin(this, "geo", [ VAR_TYPE_GEOMETRY, VAR_TYPE_ARRAY ]),
		]

		this.params.add(NodeParam("r", Vector3(0, 0, 0)))
		this.params.add(NodeParam("anchor", "none", { "combo" : [ "none", "x_min", "x_max" ] }))		

		this.layout()
	}

	calc_value(idx)
	{
		var in_geo = Blueprint.calc_input_value(this, 0)
		if (!in_geo) {
			return nil
		}

		var out_geo = GeoHelper.clone_geo(in_geo)

		var r = this.query_param("r").value

		var v_x = Blueprint.calc_input_value(this, "x")
		if (v_x) {
			r.x = v_x.value
		}
		var v_y = Blueprint.calc_input_value(this, "y")
		if (v_y) {
			r.y = v_y.value
		}
		var v_z = Blueprint.calc_input_value(this, "z")
		if (v_z) {
			r.z = v_z.value
		}

		this.rotate_scope(out_geo, r)

		return out_geo
	}

	rotate_scope(v_geo, r)
	{
		if (v_geo.type == VAR_TYPE_ARRAY)
		{
			for (var geo in v_geo.value) {
				this.rotate_scope(geo, r)
			}
		}
		else if (v_geo.type == VAR_TYPE_GEOMETRY)
		{
			if (v_geo.value.scope) 
			{
				var mat = Matrix44.init()

				var anchor = this.query_param("anchor").value
				if (anchor == "x_min")
				{
					mat.translate(0.5, 0, 0)
					mat.rotate(r.x, r.y, r.z)
					mat.translate(-0.5, 0, 0)
				}
				else if (anchor == "x_max")
				{
					mat.translate(-0.5, 0, 0)
					mat.rotate(r.x, r.y, r.z)
					mat.translate(0.5, 0, 0)
				}
				else
				{
					mat.rotate(r.x, r.y, r.z)
				}

				mat.transform_mat4(v_geo.value.scope)

				v_geo.value.scope = mat
			}
		}
	}
}
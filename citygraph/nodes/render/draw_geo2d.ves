import "blueprint.node" for Node
import "blueprint.pin" for Pin
import "blueprint.blueprint" for Blueprint
import "blueprint.variant" for Variant, VAR_TYPE_PORT, VAR_TYPE_ARRAY
import "blueprint.variant_proxy" for VariantProxy
import "blueprint.node_param" for NodeParam
import "geograph.nodes.draw_geometry" for DrawGeometry
import "geograph.variant" for VAR_TYPE_GEO, VarGeo
import "geograph.config" for POINT_RADIUS
import "citygraph.variant" for VAR_TYPE_STREETS, VAR_TYPE_BLOCK
import "citygraph.style" for BLOCK_COLORS, COMMON_BLOCK_COLOR
import "maths.vector" for Vector3
import "graphics" for Graphics
import "geometry" for Circle
import "math" for Math
import "maths" for Matrix44

class DrawGeo2d is Node
{
	init()
	{
		super.init()
	
		this.imports = [
			Pin(this, "prev", VAR_TYPE_PORT),
			Pin(this, "objs", VAR_TYPE_ARRAY),
		]
		this.exports = [
			Pin(this, "next", VAR_TYPE_PORT)
		]

		this.params.add(NodeParam("faces", false))

		this.layout()

		this.cam_mat = Matrix44.init()
		var w = Graphics.get_width()
		var h = Graphics.get_height()
		this.cam_mat.scale(w, h, 1.0)
		this.cam_mat.translate(-w/2, -h/2, 0.0)

		this.draw_geo_node = DrawGeometry()

		this.v_geos = Variant([])
		Blueprint.connect(VariantProxy(this.v_geos), 0, this.draw_geo_node, "geos")

		Blueprint.connect(VariantProxy(this.cam_mat), 0, this.draw_geo_node, "mat")

		this.need_rebuild = false
	}

	on_pin_dirty(pin)
	{
		this.need_rebuild = true
	}

	on_node_dirty()
	{
		this.need_rebuild = true
	}

	render_run()
	{
		if (this.need_rebuild) {
			this.rebuild_geos()
			this.need_rebuild = false
		}

		this.draw_geo_node.render_run()
	}

	rebuild_geos()
	{
		this.v_geos.value.clear()

		var v_objs = Blueprint.calc_input_value(this, 1)
		this.build_geos(v_objs)
	}

	build_geos(v_objs)
	{
		if (!v_objs) {
			return
		}

		if (v_objs.value is List)
		{
			for (var item in v_objs.value) {
				this.build_geos(item)
			}
		}
		else
		{
			if (v_objs.type == VAR_TYPE_STREETS)
			{
				var v_streets = v_objs.value			
				var faces = this.query_param("faces").value
				if (faces)
				{
					// blocks
					var blocks = v_streets.streets.get_polygons()
					this.build_blocks(blocks)
				}
				else
				{
					// edges
					var major = v_streets.streets.get_major_paths()
					this.build_routes(major, Vector3(1.0, 0.0, 0.0))
					var minor = v_streets.streets.get_minor_paths()
					this.build_routes(minor, Vector3(0.0, 1.0, 0.0))

					// nodes
					var nodes = v_streets.streets.get_nodes()
					this.build_crossings(nodes, Vector3(0.0, 0.0, 1.0))
				}
			}
			else if (v_objs.type == VAR_TYPE_BLOCK)
			{
				var geo = VarGeo()
				geo.shape = v_objs.value.poly
				geo.render_style["color"] = Vector3(0, 0, 0)
				this.v_geos.value.add(Variant(VAR_TYPE_GEO, geo))
			}
			else
			{
				this.v_geos.value.add(v_objs)
			}
		}
	}

	build_crossings(nodes, color)
	{
		for (var i in 0 .. nodes.count/2) 
		{
			var x = nodes[i * 2]
			var y = nodes[i * 2 + 1]

			var geo = VarGeo()
			geo.shape = Circle.init(x, y, POINT_RADIUS)
			geo.render_style["fill"] = true
			geo.render_style["color"] = color

			this.v_geos.value.add(Variant(VAR_TYPE_GEO, geo))
		}
	}

	build_routes(paths, color)
	{
		for (var path in paths)
		{
			var geo = VarGeo()
			geo.shape = path
			geo.render_style["color"] = color

			this.v_geos.value.add(Variant(VAR_TYPE_GEO, geo))
		}
	}	

	build_blocks(polygons)
	{
		for (var i in 0..polygons.count)
		{
			var geo = VarGeo()
			geo.shape = polygons[i]
			geo.render_style["color"] = BLOCK_COLORS[Math.mod(i, BLOCK_COLORS.count)]
			geo.render_style["fill"] = true

			this.v_geos.value.add(Variant(VAR_TYPE_GEO, geo))
		}
	}
}
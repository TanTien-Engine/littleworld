import "blueprint.node" for Node
import "blueprint.pin" for Pin
import "blueprint.variant" for Variant
import "blueprint.blueprint" for Blueprint
import "blueprint.node_param" for NodeParam
import "geograph.variant" for VAR_TYPE_GEO, VarGeo
import "citygraph.variant" for VAR_TYPE_LOTS
import "geometry" for Polygon
import "maths.vector" for Vector3
import "citygen" for GeometryTools

class Offset is Node
{
	init()
	{
		super.init()
	
		this.imports = [
			Pin(this, "lots", VAR_TYPE_LOTS),
		]
		this.exports = [
			Pin(this, "lots", VAR_TYPE_LOTS),
		]

		this.params.add(NodeParam("distance", 0.002, { "region" : 0.01 }))

		this.layout()

		this.v_lots = nil
	}

	on_node_dirty()
	{
		this.v_lots = nil
	}

	calc_value(idx)
	{
		var v_lots = Blueprint.calc_input_value(this, 0)
		if (!v_lots) {
			return nil
		}

		if (!this.v_lots) {
			this.v_lots = Variant(this.build_lots(v_lots.value))
		}

		return this.v_lots
	}

	build_lots(lots)
	{
		var ret = []

		var distance = this.query_param("distance").value

		for (var lot in lots)
		{
			var polygons = GeometryTools.polyline_offset(lot.value.shape, distance, true)
			if (!polygons.isEmpty)
			{
				var poly = Polygon.init()
				poly.set_vertices(polygons[0])
				for (var i in 1 .. polygons.count) {
					poly.add_hole(polygons[i])
				}

				var geo = VarGeo()
				geo.shape = poly
				geo.render_style["color"] = Vector3(0, 0, 0)
				geo.render_style["fill"] = false

				ret.add(Variant(VAR_TYPE_GEO, geo))
			}
		}

		return ret
	}	
}
import "blueprint.node" for Node
import "blueprint.pin" for Pin
import "blueprint.variant" for VAR_TYPE_ARRAY
import "blueprint.blueprint" for Blueprint
import "blueprint.variant" for Variant
import "blueprint.node_param" for NodeParam
import "rendergraph.variant" for VAR_TYPE_TEXTURE
import "citygraph.variant" for VAR_TYPE_STREETS, VarStreets
import "geograph.variant" for VAR_TYPE_GEO, VarGeo
import "maths.vector" for Vector3
import "citygen" for Streets as cg_Streets

class Streets is Node
{
	init()
	{
		super.init()
	
		this.imports = [
			Pin(this, "tensor",  VAR_TYPE_TEXTURE),
			Pin(this, "regions", VAR_TYPE_ARRAY),
		]
		this.exports = [
			Pin(this, "streets", VAR_TYPE_ARRAY),
			Pin(this, "blocks",  VAR_TYPE_ARRAY),			
		]

		this.params.add(NodeParam("num", 2, {  "integer" : true, "region" : 10 }))
		this.params.add(NodeParam("seed", 0))

		this.layout()

		this.v_streets = nil
	}

	on_node_dirty()
	{
		this.v_streets = nil
	}

	calc_value(idx)
	{
		if (!this.v_streets) {
			this.rebuild_streets()
		}

		if (idx == 0) 
		{
			return Variant(this.v_streets)
		} 
		else if (idx == 1) 
		{
			var v_blocks = []
			this.build_blocks(this.v_streets, v_blocks)
			return Variant(v_blocks)
		}

		return nil
	}

	rebuild_streets()
	{
		var v_tex = Blueprint.calc_input_value(this, 0)
		if (!v_tex or !v_tex.value) {
			return nil
		}
	
		this.v_streets = []

		var v_regions = Blueprint.calc_input_value(this, 1)

		var regions = []
		if (v_regions and v_regions.value) 
		{
			if (v_regions.value is List) {
				for (var b in v_regions.value) {
					regions.add(b.value.shape)
				}
			} else {
				regions.add(regions.value.shape)
			}
		}

		if (regions.isEmpty) {
			regions.add(nil)
		}

		var seed = this.query_param("seed").value
		var num = this.query_param("num").value
		for (var border in regions)
		{
			var st = cg_Streets.init(v_tex.value.tex, border)

			st.set_seed(seed)
			st.build_streamlines(num)

			st.build_topology()

			var v_st = VarStreets(st)
			v_st.is_major = v_regions == nil

			this.v_streets.add(Variant(VAR_TYPE_STREETS, v_st))
		}
	}

	build_blocks(v_streets, v_blocks)
	{
		if (!v_streets) {
			return
		}

		if (v_streets.value is List)
		{
			for (var item in v_streets.value) {
				this.build_blocks(item, v_blocks)
			}
		}
		else
		{
			var blocks = v_streets.value.streets.get_polygons()
			for (var block in blocks)
			{
				var geo = VarGeo()
				geo.shape = block
				geo.render_style["color"] = Vector3(0, 0, 0)
				geo.render_style["fill"] = false
				v_blocks.add(Variant(VAR_TYPE_GEO, geo))
			}
		}
	}
}